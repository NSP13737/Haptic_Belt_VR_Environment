
Shader "Unlit/FadeToBlack"
{
    Properties {
        _Alpha ("Transparency", Range(0, 1)) = 0.2
        _Color ("Color", Color) = (0, 0, 0, 1)
    }
    SubShader {
        // Render after most things, and specifically after the exception material
        Tags { "Queue" = "Overlay" "IgnoreProjector" = "True" "RenderType" = "Transparent" }
        LOD 100

        // Skip pixels where the exception material wrote stencil=1
        Stencil {
            Ref 1
            Comp NotEqual    // only draw where stencil != 1
            Pass Keep
        }

        Pass {
            Cull Off
            Blend SrcAlpha OneMinusSrcAlpha
            ZTest Always
            ZWrite Off

            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            struct appdata {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
                UNITY_VERTEX_INPUT_INSTANCE_ID
            };

            struct v2f {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
                UNITY_VERTEX_OUTPUT_STEREO
            };

            float4 _Color;
            float _Alpha;

            v2f vert(appdata v) {
                v2f o;
                UNITY_SETUP_INSTANCE_ID(v);
                UNITY_INITIALIZE_OUTPUT(v2f, o);
                UNITY_INITIALIZE_VERTEX_OUTPUT_STEREO(o);
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                return o;
            }

            fixed4 frag(v2f i) : SV_Target {
                UNITY_SETUP_STEREO_EYE_INDEX_POST_VERTEX(i);
                fixed4 color = _Color;
                color.a = _Alpha;
                return color;
            }
            ENDCG
        }
    }
}
